plugins { id "com.github.johnrengelman.shadow" version "2.0.1"; id "org.sonarqube" version "2.6.1" }
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group = 'br.cielo.cieloecommerce'
version = '0.0.1-SNAPSHOT'

apply plugin: 'maven'
apply plugin: 'java-library'

configurations.all {
    // afin de récupérer la dernière snapshot à chaque fois
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

sourceCompatibility = 1.8

task printVersion {
    doLast {
        println version
    }
}

sonarqube {
    properties {
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
    }
}

shadowJar {
    classifier = ''
    dependencies {
        exclude(dependency('com.payline:payment-method-api'))
        exclude(dependency('org.apache.logging.log4j:log4j-api'))
        exclude(dependency('org.apache.logging.log4j:log4j-core'))
    }
}

publishing {
    repositories {
        maven {
            if (project.version.endsWith('-SNAPSHOT')) {
                url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-snapshot/"
            } else {
                url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-public/"
            }
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-public/"
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
    maven {
        url "https://nexus.intra.monext.fr:10130/nexus/content/groups/public"
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
    maven {
        url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-snapshot/"
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
}


dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version:'2.6'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.5.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version:'2.8.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version:'2.8.2'
    testCompile group: 'junit', name: 'junit', version:'3.8.1'
}

assemble.dependsOn shadowJar

jar.manifest {
    attributes('Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Built-By': System.getProperty('user.name'),
            'Built-JDK': System.getProperty('java.version'),
            'Source-Compatibility': project.sourceCompatibility,
            'Target-Compatibility': project.targetCompatibility
    )
}

test.exclude '**/*IT.class'

